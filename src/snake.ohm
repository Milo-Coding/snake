snake {
  Program     =  Stmt+
  Stmt        =  newline			     	-- emptyLine
              |  Dec newline                -- declaration
              |  Assignment newline         -- assignment
              |  Call newline               -- call
              |  break newline              -- break
              |  return Exp? newline        -- return
              |  print Args newline         -- print
              |  if Exp Block (else if Exp Block)* (else Block)?  -- if
              |  while Exp Block            -- while
  
  Assignment  =  Var "is" Exp
  Dec         =  VarDec | FunDec

  Type        =  boolean | string | number
              
  VarDec      =  Type id ("is" Exp)?
  FunDec      =  function id "(" Params ")" "outputs" (void | Type) Block
  Params      =  Type id "," Params         -- multiple
              |  Type id                    -- single
              |                             -- none
  Block       =  "{" Stmt* "}"
  
  Exp         =  NonemptyListOf<Exp1, "or"> 
  Exp1        =  NonemptyListOf<Exp2, "and">
  Exp2        =  NonemptyListOf<Exp3, "^">
  Exp3        =  Exp4 relop Exp4            -- binary
              |  Exp4
  Exp4        =  Exp4 addop Exp5            -- binary
              |  "-" Exp5                   -- unary
              |  Exp5
  Exp5        =  Exp5 mulop Exp6            -- binary
              |  Exp6
  Exp6        =  Literal
              |  Var
              |  NewList
              |  EmptyList
              |  "(" Exp ")"                -- parens
  Literal     =  null
              |  true
              |  false
              |  numberlit
              |  stringlit
  Var         =  Var "[" Exp "]"            -- subscript
              |  Var "." id                 -- property
              |  Call
              |  id

  NewList     =  new list "[" Args "]"
  EmptyList   =  new list "[" "]"
  Call        =  id "(" Args ")"
  Args        =  ListOf<Exp, ",">

  boolean     = "truth_value" ~idchar
  break       = "stop_loop" ~idchar
  else        = "else" ~idchar
  false       = "false" ~idchar
  for         = "for" ~idchar
  if          = "if" ~idchar
  number      = "number" ~idchar
  new         = "new" ~idchar
  null        = "no_value" ~idchar
  print       = "print" ~idchar
  return      = "return" ~idchar
  string      = "text" ~idchar
  true        = "true" ~idchar
  void        = "void" ~idchar
  while       = "loop_while" ~idchar
  function    = "reusable_code" ~idchar
  list        = "list" ~idchar

  keyword     =  boolean | if | break | else | number | for | new
              |  return | null | while | true | string | function
              | void | false | print
              
  id          =  ~keyword letter idchar*
  idchar      =  "_" | alnum
  numberlit   =  digit+ "."? digit* (("E"|"e") ("+"|"-")? digit+)?
  stringlit   =  "\"" (~"\"" any)* "\""
  
  addop       =  "+" | "-"
  relop       =  "<=?" | "<?" | "=?" | "!=?" | ">=?" | ">?"
  mulop       =  "*" | "/" | "modulus"
  
  newline     =  "\n" | "\r\n"

  space      :=  " " | "\t" | comment
  comment     =  "//" (~"\n" any)*
}